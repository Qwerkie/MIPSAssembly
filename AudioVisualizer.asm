.data
	pitch: .word 	76, 1, 76, 1, 72, 76, 1, 79, 0, 67, 1, 72, 1, 67, 1, 64, 1, 69, 1, 71, 1, 70, 69, 1, 67, 76, 79, 81, 1, 77, 79, 1, 76, 1, 72, 74, 71, 1, 60, 0, 79, 78, 77, 75, 60, 76, 53, 68, 69, 72, 60, 69, 72, 74, 48, 1, 79, 78, 77, 75, 55, 76, 20, 84, 20, 84, 84,
			55, 20, 48, 20, 79, 78, 77, 75, 60, 76, 53, 68, 69, 72, 60, 69, 72, 74, 48, 20, 75, 20, 74, 20, 72, 20, 55, 55, 20, 48, 72, 72, 20, 72, 20, 72, 74, 20, 76, 72, 20, 69, 67, 20, 43, 20, 72, 72, 20, 72, 20, 72, 74, 76, 55, 20, 48, 20, 43, 20, 72, 72, 20, 72, 20, 72, 74, 20, 76, 72, 20, 69, 67, 20, 43, 20, 76, 76, 20, 76, 20, 72, 76, 20, 79, 20, 67, 20,
			72, 1, 67, 1, 64, 1, 69, 1, 71, 1, 70, 69, 1, 67, 76, 79, 81, 1, 77, 79, 1, 76, 1, 72, 74, 71, 1, 76, 72, 20, 67, 55, 20, 68, 20, 69, 77, 53, 77, 69, 60, 53, 20, 71, 81, 81, 81, 79, 77, 76, 72, 55, 69, 67, 60, 55, 20, 76, 72, 20, 67, 55, 20, 68, 20, 69, 77, 53, 77, 69, 60, 53, 20, 71, 77, 20, 77, 77, 76, 74, 72, 64, 55, 64, 60, 20,
			72, 20, 67, 20, 64, 69, 71, 69, 68, 70, 68, 67, 65, 67
	time:	.word	120, 120, 120, 120, 120, 120, 120, 120, 360, 120, 360, 120, 240, 120, 240, 120, 240, 120, 120, 120, 120, 120, 120, 120, 160, 160, 160, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 240, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120,
			120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 240, 120, 240, 120, 240, 120, 120, 120, 120, 120, 
			120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 240, 120, 240, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 360, 120, 360,
			120, 240, 120, 240, 120, 240, 120, 120, 120, 120, 120, 120, 120, 160, 160, 160, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 240,
			120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 160, 160, 160, 160, 160, 160, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 120, 160, 160, 160, 120, 120, 120, 120, 120, 360,
			120, 240, 120, 240, 240, 160, 160, 160, 240, 240, 240, 120, 120, 480
	
	DisplayTable:
		ycoord:
			.word  	1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
		colors:
			.word  	0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
		length:
			.word  	1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
	
	ColorTable:
		.word 0x000000		#black
		.word 0x0000ff		#blue
		.word 0x00ff00		#green
		.word 0xff0000		#red
		.word 0x00ffff		#blue + green
		.word 0xff00ff		#blue + red
		.word 0xffff00		#green + red
		.word 0xffffff		#white
		
	stack_beg:
		.word 0:100
	stack_end:
	MainMenuText:	.asciiz	"\n\nPlease select a method to play music \n 1)Manually \n 2)Auto \n 3)Clear Display \n 0)Exit \n ::"
	ErrorText:	.asciiz	"\nInvalid Input, try again\n ::"
	ManualEntryText:.asciiz	"\n\nPress 1-9 to play notes\nPress 0 to exit\n ::"
	ClearText:	.asciiz "\n\nWould you like to clear the display when the bars hit the end of the screen? \n 1) Yes \n 2) No \n ::"
.text

	la $sp, stack_end
	jal ClearDisp

main:
	li $t4, 0
	la $s0, ycoord
	la $s1, colors
	la $s2, length
	

	jal MainMenu
	

#Checking Input	
	bne $v0, 49, auto
	jal ClearChoice
	jal ManualEntry
	j main
	
auto:	bne $v0, 50, clear
	jal ClearChoice
	jal AutoEntry
	j main

clear:	bne $v0, 51, E
	jal ClearDisp
	j main
	
E:	bne $v0, 48, error
	j Exit
	
error:	li $v0, 4
	la $a0, ErrorText
	syscall
	jal main
		
Exit: 
	li $v0, 10
	syscall	
	
#############################################	
#takes in nothing
#sends back the choice into $t5
ClearChoice:
la $a0, ClearText
li $v0, 4
syscall
li $v0, 12
syscall
move $t5, $v0
jr $ra




#############################################	
#Main Menu stuff
# Who will play the music?
# 1: Self
# 2: Automatic
MainMenu:	
#Takes nothing in!
#Gives back the user choice
	li $v0, 4
	la $a0, MainMenuText
	syscall
	li $v0, 12
	syscall
jr $ra

#############################################
# first load in the necessary values to play the tone syscall
# 1 - 9 are the notes you can play
# 0 to exit
# anything else will result in error
ManualEntry:
# Takes in nothing
# Returns nothing
	addiu $sp, $sp, -4
	sw $ra, ($sp)
	la $a0, ManualEntryText
	li $v0, 4
	syscall
	li $t4, 31
MentryLoop:	li $v0, 12
		syscall
		move $a0, $v0
		jal PlayUserSound
		jal DrawLine
		bne $v0, 48, MentryLoop
	lw $ra, ($sp)
	addiu $sp, $sp, 4
jr $ra

#############################################
# Doesnt take anything in
# plays through premade song
# goes back to main menu
AutoEntry:
	addiu $sp, $sp, -4
	sw $ra, ($sp)

	li $v0, 33
	la $t0, pitch
	la $t1, time
	li $a2, 70
	li $a3, 50
	li $t7, 0
	li $t4, 31
	#Song Loop
	loop1:
		lw $a0, ($t0)
		lw $a1, ($t1)
		syscall
		jal DrawLine
		addiu $t0, $t0, 4
		addiu $t1, $t1, 4
		addiu $t7, $t7, 1
		blt $t7, 256, loop1	#256
	
	lw $ra, ($sp)
	addiu $sp, $sp, 4
jr $ra

#############################################
# Takes in the user choice ($v0) and translates that to the sound to play 
PlayUserSound:
	addiu $sp, $sp, -4
	sw $v0, ($sp)

	#Setting needed values
	la $v0, 33
	la $a1, 200
	la $a2, 70
	la $a3, 50
	
	#Determine what note to play

	bgt $a0, 57, done # > 9
	beq $a0, 48, done # = 0
	
nineL:	blt $a0, 57, eightL
	li $a0, 85
	j play
	
eightL:	blt $a0, 56, sevenL
	li $a0, 80
	j play
	
sevenL:	blt $a0, 55, sixL
	li $a0, 75
	j play
	
sixL:	blt $a0, 54, fiveL
	li $a0, 70
	j play
	
fiveL:	blt $a0, 53, fourL
	li $a0, 65
	j play
	
fourL:	blt $a0, 52, threeL
	li $a0, 60
	j play
	
threeL:	blt $a0, 51, twoL
	li $a0, 55
	j play
	
twoL:	blt $a0, 50, oneL
	li $a0, 50
	j play
	
oneL:	blt $a0, 49, erroruser
	li $a0, 45
	j play


erroruser:
	la $a0, ErrorText
	li $v0, 4
	syscall
	j done
	
play:	syscall

done:
lw $v0, ($sp)
addiu $sp, $sp, 4
jr $ra

	
#############################################
# Takes in the pitch and converts it to the digital display
#$a0 = x coord (0-31)
#$a1 = y coord (0-31)
#$a2 = color number (0-7)
#$a3 = length of the line (1-32)
DrawLine:
	addiu $sp, $sp -32
	sw $ra, ($sp)
	sw $t0, 4($sp)
	sw $t1, 8($sp)
	sw $t7, 12($sp)
	sw $v0, 16($sp)
	sw $a2, 20($sp)
	sw $a3, 24($sp)
	sw $t5, 28($sp)
	
	move $t6, $a0
	
	#color
randomColor:
	li $v0, 42
	li $a1, 6
	syscall
	addiu $a0, $a0, 1
	move $a2, $a0
	

	
	#y coord
nineLi:		blt $t6, 85, eightLi 
		li $a1, 1
		j nextLI
		
eightLi:	blt $t6, 80, sevenLi
		li $a1, 4
		j nextLI
		
sevenLi:	blt $t6, 75, sixLi
		li $a1, 8
		j nextLI
		
sixLi: 		blt $t6, 70, fiveLi
		li $a1, 12
		j nextLI
		
fiveLi: 	blt $t6, 65, fourLi
		li $a1, 16
		j nextLI
		
fourLi: 	blt $t6, 60, threeLi
		li $a1, 20
		j nextLI
		
threeLi: 	blt $t6, 55, twoLi
		li $a1, 24
		j nextLI
		
twoLi: 		blt $t6, 50, oneLi
		li $a1, 28
		j nextLI
		
oneLi:		li $a1, 31
	
		beq $t6, 48, doneLI
nextLI:		
		#getting the length
		li $t5, 32
		sub $a3, $t5, $a1
	lw $t5, 28($sp)
	#####################
	#Make the line

	bgtz $t4, draw
	beq $t5, 49, reset
	li $t4, 32
	j draw

reset:	jal ClearDisp
	li $t4, 32
	
draw:	move $a0, $t4
	jal VertLine
	addiu $t4, $t4, -1

doneLI:

	lw $ra, ($sp)
	lw $t0, 4($sp)
	lw $t1, 8($sp)
	lw $t7, 12($sp)
	lw $v0, 16($sp)
	lw $a2, 20($sp)
	lw $a3, 24($sp)
	addiu $sp, $sp, 32
jr $ra
	
#############################################
#Graphics stuff

Convert_Coord:
#$a0 = X coordinate (0 - 31)
#$a1 = Y coordinate (0 - 31)
#Returns $v0 = memory address
	CalcAddress:
		mul $t0, $a0, 4		#X coord
		mul $t1, $a1, 128	#Y coord
		li $t2, 0x10040000
		add $v0, $t0, $t1
		add $v0, $v0, $t2
jr $ra

LookupColor:
#$a2 = color number (0 - 7)
#returns $v1 = actual number to write tot he display
	GetColor:
		la $t1, ColorTable
		sll $a2, $a2, 2		#index x4 is offset
		addu $a2, $a2, $t1	#base + offset
		lw $v1, 0($a2)		#get actual color word
jr $ra

Draw_A_Dot:
#$a0 = x coordinate (0 - 31)
#a1 = y coordinate (0 - 31)
#a2 = color number (0 - 7)
	DrawDot:
		addiu $sp, $sp, -8	#room for two
		sw $ra, 4($sp)
		sw $a2, ($sp)
		jal CalcAddress
		lw $a2, ($sp)
		sw $v0, ($sp)
		jal GetColor
		lw $v0, ($sp)
		sw $v1, ($v0)		#make dot
		lw $ra, 4($sp)
		addiu $sp, $sp, 8
jr $ra

Draw_Vert_Line:
#$a0 = x coord (0-31)
#$a1 = y coord (0-31)
#$a2 = color number (0-7)
#$a3 = length of the line (1-32)
	VertLine:
		addiu $sp, $sp, -20
		sw $ra, ($sp)
	VertLoop:
		#stack a
		sw $a0, 4($sp)
		sw $a1, 8($sp)
		sw $a2, 12($sp)
		sw $a3, 16($sp)
		
		#draw dot
		jal DrawDot
		
		#Unstack a
		lw $a0, 4($sp)
		lw $a1, 8($sp)
		lw $a2, 12($sp)
		lw $a3, 16($sp)
		
		#increment x coord = a0
		addiu $a1, $a1, 1
		
		#decriment the length and branch until 0
		addiu $a3, $a3, -1
		bne $a3, $0, VertLoop
		
		lw $ra, ($sp)
		addiu $sp, $sp, 20
jr $ra

Draw_Filled_Box:
#$a0 = x coord (0-31)
#$a1 = y coord (0-31)
#$a2 = color number (0-7)
#$a3 = size of the box (1-32)
	DrawBox:
		addiu $sp, $sp, -24
		sw $ra, ($sp)
		add $t0, $0, $a3
	BoxLoop:
		#stack a
		sw $a0, 4($sp)
		sw $a1, 8($sp)
		sw $a2, 12($sp)
		sw $a3, 16($sp)
		sw $t0, 20($sp)
		
		jal HorzLine
		
		#Unstack a
		lw $a0, 4($sp)
		lw $a1, 8($sp)
		lw $a2, 12($sp)
		lw $a3, 16($sp)
		lw $t0, 20($sp)
		
		addiu $a1, $a1, 1	#inc y coord
		addiu $t0, $t0, -1	#dec counter
		bne $t0, $0, BoxLoop
	
	lw $ra, ($sp)
	addiu $sp, $sp, 24

jr $ra

ClearDisplay:
#Draw a black box over whole display
	ClearDisp:
		addiu $sp, $sp, -4
		sw $ra, ($sp)
		
		#Blank the screen
		li $a0, 0
		li $a1, 0
		li $a2, 0
		li $a3, 32
		jal DrawBox
		
								
		lw $ra, ($sp)
		addiu $sp, $sp, 4
jr $ra

Draw_Horz_Line:
#$a0 = x coord (0-31)
#$a1 = y coord (0-31)
#$a2 = color number (0-7)
#$a3 = length of the line (1-32)
	HorzLine:
		addiu $sp, $sp, -20
		sw $ra, ($sp)
	HorzLoop:
		#stack a
		sw $a0, 4($sp)
		sw $a1, 8($sp)
		sw $a2, 12($sp)
		sw $a3, 16($sp)
		
		#draw dot
		jal DrawDot
		
		#Unstack a
		lw $a0, 4($sp)
		lw $a1, 8($sp)
		lw $a2, 12($sp)
		lw $a3, 16($sp)
		
		#increment x coord = a0
		addiu $a0, $a0, 1
		
		#decriment the length and branch until 0
		addiu $a3, $a3, -1
		bne $a3, $0, HorzLoop
		
		lw $ra, ($sp)
		addiu $sp, $sp, 20
jr $ra








